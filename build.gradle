buildscript {
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.0'
    id 'org.ajoberstar.grgit' version '4.0.2'
    id 'maven-publish'
}

repositories {
    jcenter()
    mavenCentral()
}

def localProperties = new Properties()
def propertiesFile = rootProject.file('local.properties')
if (propertiesFile.exists()) {
    localProperties.load(propertiesFile.newDataInputStream())
}
group = 'nl.blackstardlb'
version = '0.0.3'

def getVcpkgInstallDir(vcpkgDir) {
    def dirName;
    if (isLinux) {
        dirName = "x64-linux"
    } else if (isMacos) {
        dirName = "x64-osx"
    } else if (isWindows) {
        dirName = "x64-windows"
    } else throw new GradleException("Host OS is not supported in Kotlin/Native.")
    return file("$vcpkgDir/installed/$dirName")
}

ext {
    hostOs = System.getProperty("os.name")
    isLinux = hostOs == "Linux"
    isMacos = hostOs == "Mac OS X"
    isWindows = hostOs.startsWith("Windows")
    gumbo_def_file = new File(projectDir, "src/nativeInterop/cinterop/gumbo.def")
    test_resources_folder = new File(projectDir, "src/commonTest/resources")
    bintrayUser = localProperties.get("bintrayUser")
    bintrayApiKey = localProperties.get("bintrayApiKey")

    vcpkg_dir = new File(buildDir, "vcpkg")
    vcpkg_script = new File(vcpkg_dir, "vcpkg")
    vcpkg_repo = "https://github.com/microsoft/vcpkg.git"
    vcpkg_branch = "origin/master"
    vcpkg_install_dir = getVcpkgInstallDir(vcpkg_dir)
}

task generateDefFile() {
    if (!gumbo_def_file.exists()) {
        gumbo_def_file.createNewFile()
        gumbo_def_file.text = """headers = include/gumbo.h
compilerOpts = -I$vcpkg_install_dir
staticLibraries.linux = libgumbo.a
staticLibraries.osx = libgumbo.a
staticLibraries.ios = libgumbo.a
libraryPaths = $vcpkg_install_dir/lib 
"""
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    def test = taskGraph.getAllTasks().find { it.name == "generateMetadataFileForKotlinMultiplatformPublication" }
    if (test != null) {
        def nTarget = kotlin.targets.find { it.name == "native" }
        kotlin.targets.remove(nTarget)
    }
    taskGraph.getAllTasks().each {
        if ((it.name.contains("Native") || it.name.contains("native")) && it.name != "cinteropGumboNative") {
            it.onlyIf { false }
        }
        if (!isLinux) {
            if (it.name.contains("Linux") || it.name.contains("linux")) {
                it.onlyIf { false }
            }
        }
    }
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
    }

    iosX64() {
        compilations.main.cinterops {
            gumbo
        }
    }

    iosArm64() {
        compilations.main.cinterops {
            gumbo
        }
    }

    macosX64('macos') {
        compilations.main.cinterops {
            gumbo
        }
    }

    linuxX64("linux") {
        compilations.main.cinterops {
            gumbo
        }
    }

    org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTargetWithTests nativeTarget
    if (isMacos) nativeTarget = macosX64('native')
    else if (isLinux) nativeTarget = linuxX64("native")
    else if (isWindows) nativeTarget = mingwX64("native")
    else throw new GradleException("Host OS is not supported in Kotlin/Native.")

    nativeTarget.compilations.main.cinterops {
        gumbo
    }

    configure([targets["metadata"], jvm()]) {
        mavenPublication { targetPublication ->
            tasks.withType(AbstractPublishToMaven)
                    .matching { it.publication == targetPublication }
                    .all { onlyIf { isLinux } }
        }
    }
    sourceSets {
        commonMain {

        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        nativeTest {
            dependsOn(commonTest)
        }

        linuxMain.kotlin.srcDirs = [file("src/nativeMain")]
        linuxTest.kotlin.srcDirs = [file("src/nativeTest")]
        macosMain.kotlin.srcDirs = [file("src/nativeMain")]
        macosTest.kotlin.srcDirs = [file("src/nativeTest")]
        iosX64Main.kotlin.srcDirs = [file("src/nativeMain")]
        iosX64Test.kotlin.srcDirs = [file("src/nativeTest")]
        iosArm64Main.kotlin.srcDirs = [file("src/nativeMain")]
        iosArm64Test.kotlin.srcDirs = [file("src/nativeTest")]

        jvmMain {
            dependencies {
                implementation 'org.jsoup:jsoup:1.13.1'
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
    }
}

task cleanGumboDef(type: Delete) {
    delete(gumbo_def_file)
    tasks.clean.dependsOn(cleanGumboDef)
}

def getBintrayUserProperty() {
    return bintrayUser != null ? bintrayUser : System.getenv('BINTRAY_USER')
}

def getBintrayApiKeyProperty() {
    return bintrayApiKey != null ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
}

task cleanVcpkg(type: Delete) {
    delete(vcpkg_dir)
    tasks.clean.dependsOn(cleanVcpkg)
}

task cloneVcpkg() {
    doLast {
        def gr = org.ajoberstar.grgit.Grgit.clone(dir: "$vcpkg_dir", uri: "$vcpkg_repo")
        gr.checkout(branch: "$vcpkg_branch")
    }
    onlyIf { !vcpkg_dir.exists() }
}

task bootstrapVcpkg(type: Exec, dependsOn: "cloneVcpkg") {
    workingDir vcpkg_dir
    commandLine "$vcpkg_dir/bootstrap-vcpkg.sh"
    onlyIf { !file("$vcpkg_dir/downloads").exists() }
}

task makeGumbo(type: Exec) {
    dependsOn "bootstrapVcpkg"
    workingDir vcpkg_dir
    commandLine "$vcpkg_script", "install", "gumbo"
    onlyIf { !file("$vcpkg_install_dir/lib/libgumbo.a").exists() }
}

tasks.each {
    if (it.name.startsWith("cinteropGumbo")) {
        it.dependsOn("makeGumbo")
    }
}

publishing {
    repositories {
        maven {
            name = "Bintray"
            url = uri("https://api.bintray.com/maven/blackstardlb/GumboSoup/gumbo-soup/;publish=1;override=1")
            credentials {
                username = getBintrayUserProperty()
                password = getBintrayApiKeyProperty()
            }
        }
    }
}

tasks.each {
    if (it.class.name == "org.jetbrains.kotlin.gradle.targets.native.tasks.KotlinNativeHostTest_Decorated") {
        it.doFirst {
            environment 'RESOURCES_FOLDER', "$test_resources_folder/"
        }
    }
    if (it.class.name == "org.jetbrains.kotlin.gradle.targets.native.tasks.KotlinNativeSimulatorTest_Decorated") {
        it.doFirst {
            environment 'SIMCTL_CHILD_RESOURCES_FOLDER', "$test_resources_folder/"
        }
    }
}
